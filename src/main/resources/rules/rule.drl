package rules
dialect "java"

import com.baltan.drools.pojo.User
import com.baltan.drools.pojo.Lover
import com.baltan.drools.pojo.Loser
import com.baltan.drools.pojo.Context
import com.baltan.drools.pojo.Random
import com.baltan.drools.pojo.Query
import org.kie.api.runtime.KieRuntime

rule "rule1"
when
    // 变量名:变量类型(匹配规则)
    // $d:Double(this == 0)
    $d : Double($d == 0)
then
    System.out.println("***********" + $d + "***********");
end

rule "rule2"
when
    $u : User($u.name == "Baltan" && $u.age == 30)
then
    System.out.println("***********" + $u + "***********");
end

// 数组中索引为1处的元素是否是"sleeping"
rule "rule3"
when
    $u : User(hobbies != null && hobbies[1] == "sleeping")
then
    System.out.println("***********" + $u + "***********");
end

// tags中"终生代号"这个key对应的值是否是9527
rule "rule4"
when
    // $u:User(tags.get("终生代号") == 9527)
    $u : User(tags != null && tags["终生代号"] == 9527)
then
    System.out.println("***********" + $u + "***********");
end

// "football"是否是数组hobbies中的元素
rule "rule5"
when
    $u : User(hobbies != null && hobbies contains "football")
then
    System.out.println("***********" + $u + "***********");
end

// "table tennis"是否是数组hobbies中的元素
rule "rule6"
when
    $u : User(hobbies != null && "table tennis" memberOf $u.hobbies)
then
    System.out.println("***********" + $u + "***********");
end

// 正则匹配
rule "rule7"
when
    $u : User(name matches "Zh\\w+\\s{1}S\\w+")
then
    System.out.println("***********" + $u + "***********");
end

// 遍历数组
rule "rule8"
when
    $u : User();
    $s : String($s == "coding" || $s == "sleeping") from $u.hobbies
then
    System.out.println("***********" + $u + "***********");
end

// activation-group定义组别（互斥分组），同一组别中默认排在最前面的那条被满足的规则被执行
rule "rule9"
activation-group "group1"
when
    $l : Lover(false);
then
    System.out.println("***********group1-1***********");
end

rule "rule10"
activation-group "group1"
when
    $l : Lover();
then
    System.out.println("***********group1-2***********");
end

rule "rule11"
activation-group "group1"
when
    $l : Lover();
then
    System.out.println("***********group1-3***********");
end

// salience定义同一组别中规则的优先级，优先级最高的那条被满足的规则被执行，默认值为0
rule "rule12"
activation-group "group2"
salience 1000
when
    $l : Lover(name == "B");
then
    System.out.println("***********group2-1***********");
end

rule "rule13"
activation-group "group2"
salience 888
when
    $l : Lover(name == "A");
then
    System.out.println("***********group2-2***********");
end

rule "rule14"
activation-group "group2"
salience 999
when
    $l : Lover(name == "A");
then
    System.out.println("***********group2-3***********");
end

// agenda-group组别（同步分组）中的规则需要被显式激活才会被判断，并且组别中所有被满足的规则都会被执行
// session.getAgenda().getAgendaGroup("group3").setFocus();
rule "rule15"
agenda-group "group3"
when
    $l : Lover(name == "C");
then
    System.out.println("***********" + $l + "***********");
end

rule "rule16"
agenda-group "group3"
when
    $l : Lover(name == "C");
then
    System.out.println("***********" + $l + "***********");
end

// no-loop属性配置为true后，可以避免当前规则在执行update()后被反复循环判断执行
rule "rule17"
no-loop true
when
    $l : Loser(name == "A");
then
    System.out.println("***********" + $l + "***********");
    update($l);
end

rule "rule18"
when
    $l : Loser(name == "B");
then
    System.out.println("***********" + $l + "***********");
    $l.setName("C");
    update($l);
end

// lock-on-active属性配置为true后，即使其他规则执行了更新对象或者插入对象的操作，重新判断所有规则时也会忽略当前规则，
// 但是第一次判断所有规则时，当前规则不会被忽略
rule "rule19"
lock-on-active true
when
    $l : Loser(name == "C");
then
    System.out.println("***********" + $l + "***********");
    update($l);
end

// 执行插入对象的操作后，所有的规则都会再被判断一遍
rule "rule20"
when
    $l : Loser(name == "D");
then
    System.out.println("***********" + $l + "***********");
    insert(new Loser("E"));
end

rule "rule21"
when
    $l : Loser(name == "E");
then
    System.out.println("***********" + $l + "***********");
end

// retract()方法将内存中的对象删除
rule "rule22"
when
    $l : Loser(name == "F");
then
    System.out.println("***********loser-1***********");
    // delete($l);
    // delete()方法也能执行删除对象的操作，但是IDEA不会将方法名加粗
    retract($l)
end

rule "rule23"
when
    $l : Loser(name == "F");
then
    System.out.println("***********loser-2***********");
end

// kcontext上下文对象操作
rule "rule24"
when
    $c : Context();
then
    KieRuntime kr =  kcontext.getKieRuntime();
    System.out.println("***********" + kr + "***********");
    // 激活指定的规则
    kr.getAgenda().getAgendaGroup("group4").setFocus();
end

rule "rule25"
agenda-group "group4"
when
    $c : Context();
then
    System.out.println("***********group4-1***********");
end

// 系统时间到达date-effective配置的时间，规则才可能被执行
// date-effective默认格式为dd-MMM-yyyy，如果需要修改时间格式，可以在向KnowledgeBuilder里添加规则文件之前执行：
// System.setProperty("drools.dateformat", "yyyy-MM-dd");
rule "rule26"
date-effective "27-Jun-2020"
when
    $r : Random();
then
    System.out.println("***********random-1***********");
end

rule "rule27"
date-effective "28-Jun-2020"
when
    $r : Random();
then
    System.out.println("***********random-2***********");
end

rule "rule28"
date-effective "29-Jun-2020"
when
    $r : Random();
then
    System.out.println("***********random-3***********");
end

// 系统时间到达date-effective配置的时间，规则才可能被执行
// date-expires默认格式为dd-MMM-yyyy
rule "rule29"
date-expires "27-Jun-2020"
when
    $r : Random();
then
    System.out.println("***********random-4***********");
end

rule "rule30"
date-expires "28-Jun-2020"
when
    $r : Random();
then
    System.out.println("***********random-5***********");
end

rule "rule31"
date-expires "29-Jun-2020"
when
    $r : Random();
then
    System.out.println("***********random-6***********");
end

// duration属性配置的毫秒数过后，规则会在另一个线程中被触发
rule "rule32"
duration 3000
when
    $l : Loser(name == "G")
then
    System.out.println("***********" + $l + "***********");
    System.out.println("thread id: " + Thread.currentThread().getId());
end

// auto-focus属性配置为true的规则，即使配置了agenda-group属性，不需要显式激活也会被判断（见"rule15"）
rule "rule33"
agenda-group "group5"
auto-focus true
when
    $l : Loser(name == "H")
then
    System.out.println("***********" + $l + "***********");
end

rule "rule34"
when
    $l : Loser(name == "Z")
then
    System.out.println("***********rule34***********");
end

rule "rule35"
when
    $l : Loser(name == "Z")
then
    System.out.println("***********rule35***********");
end

rule "rule36"
when
    $l : Loser(name == "Z")
then
    System.out.println("***********rule36***********");
end

rule "rule37"
when
    $l : Loser(name == "I")
then
    say(drools.getRule().getName());
end

// 函数可以在每个规则的RHS被调用
function void say(String ruleName) {
    System.out.println("Well done, " + ruleName + " is executed!");
}

query "query1"
    $q : Query(name matches "query\\d+")
end

query "query2"(String[] $names)
    $q : Query(name memberOf $names)
end